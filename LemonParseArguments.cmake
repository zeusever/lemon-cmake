include(LemonTrace)
include(CMakeParseArguments)

function(lemon_parse_arguments NAME PREFIX)
  #call the cmake_parse_arguments function
  set(MULTI_ARGS LEMON_OPTION_ARGS LEMON_ONE_VALUE_KEY LEMON_MULTI_VALUE_KEY LEMON_REQUIRED_KEYS LEMON_INPUT_ARGS)
  cmake_parse_arguments(LOCAL "" "" "${MULTI_ARGS}" ${ARGN})
  lemon_debug("~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~")
  lemon_debug("lemon_parse_arguments args:")
  lemon_debug("~ NAME :${NAME}")
  lemon_debug("~ PREFIX :${PREFIX}")
  lemon_list_print_string(RESULT ${LOCAL_LEMON_OPTION_ARGS})
  lemon_debug("~ LEMON_OPTION_ARGS:${RESULT}")
  lemon_list_print_string(RESULT ${LOCAL_LEMON_ONE_VALUE_KEY})
  lemon_debug("~ LEMON_ONE_VALUE_KEY:${RESULT}")
  lemon_list_print_string(RESULT ${LOCAL_LEMON_MULTI_VALUE_KEY})
  lemon_debug("~ LEMON_MULTI_VALUE_KEY:${RESULT}")
  lemon_list_print_string(RESULT ${LOCAL_LEMON_REQUIRED_KEYS})
  lemon_debug("~ LEMON_REQUIRED_KEYS:${RESULT}")
  lemon_list_print_string(RESULT ${LOCAL_LEMON_INPUT_ARGS})
  lemon_debug("~ LEMON_INPUT_ARGS:${RESULT}")
  lemon_debug("~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~")

  if(NOT LOCAL_LEMON_INPUT_ARGS)
    return()
  endif()

  cmake_parse_arguments(${PREFIX} "${LOCAL_LEMON_OPTION_ARGS}" "${LOCAL_LEMON_ONE_VALUE_KEY}" "${LOCAL_LEMON_MULTI_VALUE_KEY}" ${LOCAL_LEMON_INPUT_ARGS})

  lemon_debug("~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~")
  lemon_debug("~ option args:")
  foreach(ARG ${LOCAL_LEMON_OPTION_ARGS})
    lemon_debug("~  @${PREFIX}_${ARG}:${${PREFIX}_${ARG}}")  
    set(${PREFIX}_${ARG} ${${PREFIX}_${ARG}} PARENT_SCOPE)
  endforeach()
  lemon_debug("~ one value keywords:")
  foreach(ARG ${LOCAL_LEMON_ONE_VALUE_KEY})
    lemon_debug("~  @${PREFIX}_${ARG}:${${PREFIX}_${ARG}}") 
    set(${PREFIX}_${ARG} ${${PREFIX}_${ARG}} PARENT_SCOPE)   
  endforeach()
  lemon_debug("~ multi value keywords:")
  foreach(ARG ${LOCAL_LEMON_MULTI_VALUE_KEY})
    lemon_list_print_string(RESULT ${${PREFIX}_${ARG}})
    lemon_debug("~  @${PREFIX}_${ARG}:${RESULT}")    
    set(${PREFIX}_${ARG} ${${PREFIX}_${ARG}} PARENT_SCOPE)
  endforeach()
  lemon_debug("~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~")

  foreach(ARG ${LOCAL_LEMON_REQUIRED_KEYS})
    if(NOT ${PREFIX}_${ARG})
      lemon_error("function <${NAME}> not found required arg :${ARG}")
    endif()
  endforeach()

  if(${PREFIX}_UNPARSED_ARGUMENTS)
    set(${PREFIX}_UNPARSED_ARGUMENTS ${${PREFIX}_UNPARSED_ARGUMENTS} PARENT_SCOPE)
  endif()

  #unset the local varible value
  unset(MULTI_ARGS)
  unset(LOCAL_LEMON_OPTION_ARGS)
  unset(LOCAL_LEMON_ONE_VALUE_KEY)
  unset(LOCAL_LEMON_MULTI_VALUE_KEY)
  unset(LOCAL_LEMON_REQUIRED_KEYS)
  unset(LOCAL_LEMON_INPUT_ARGS)

endfunction()